#!/usr/bin/perl -w

use strict;
use Config::PHPINI;
use Proc::Daemon;
use Getopt::Long;
use POSIX qw(strftime);
use IO::Handle;

my $config = Config::PHPINI->read('/etc/opt/pelagos.ini');

my $pidfile = '/var/run/filerd.pid';
GetOptions("pidfile=s" => \$pidfile);

my $daemon = Proc::Daemon->new( pid_file => $pidfile );

$daemon->Init;

chmod 0644, $pidfile;
umask 0022;

my $logfile  = "$config->{paths}->{log}/filerd";
open(my $log,'>>',$logfile) or die "cannot open log file $logfile: $!";
$log->autoflush(1);

writelog($log,"filerd starting");

my $continue = 1;
$SIG{'TERM'} = sub { $continue = 0 };

my $run_filer = 0;
$SIG{'HUP'} = sub { $run_filer = 1 };

while ($continue) {
    while ($run_filer) {
        writelog($log,"running filer");
        $run_filer = 0;
        system('/opt/pelagos/back-end/filer/filer');
        writelog($log,"filer finished");
    }
    sleep;
}

writelog($log,"filerd shutting down");
close $log;

sub writelog {
    my ($log,$message) = @_;
    my $ts = POSIX::strftime("%Y-%m-%d %H:%M:%S", localtime);
    print $log "[$ts] $message\n";
}
